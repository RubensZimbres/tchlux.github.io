! Use the LAPACK SVD routine to only compute the singular values.
SUBROUTINE LAPACK_SING_VALS(A, S)
  USE ISO_FORTRAN_ENV, ONLY: RT => REAL32
  IMPLICIT NONE
  REAL(KIND=RT), INTENT(INOUT), DIMENSION(:,:) :: A
  REAL(KIND=RT), INTENT(OUT), DIMENSION(MIN(SIZE(A,1),SIZE(A,2))) :: S
  ! Local variables.
  CHARACTER :: JOBZ
  INTEGER   :: M
  INTEGER   :: N
  INTEGER   :: LDA
  REAL(KIND=RT), DIMENSION(1,1) :: U
  INTEGER   :: LDU
  REAL(KIND=RT), DIMENSION(1,1) :: VT
  INTEGER   :: LDVT
  REAL(KIND=RT), DIMENSION(:), ALLOCATABLE :: WORK
  INTEGER   :: LWORK
  INTEGER, DIMENSION(8*MIN(SIZE(A,1),SIZE(A,2))) :: IWORK
  INTEGER   :: INFO 
  REAL(KIND=RT), DIMENSION(1) :: WORK_SIZE
  EXTERNAL :: SGESDD
  ! Set the JOBZ to only compute singular values.
  JOBZ = 'N'
  M = SIZE(A,1)
  N = SIZE(A,2)
  LDA = M
  LDU = 1
  LDVT = 1
  LWORK = -1
  INFO = 0
  CALL SGESDD(JOBZ, M, N, A, LDA, S, U, LDU, VT, LDVT, WORK_SIZE, LWORK, IWORK, INFO)
  ALLOCATE(WORK(1:INT(WORK_SIZE(1))))
  LWORK = INT(WORK_SIZE(1))
  CALL SGESDD(JOBZ, M, N, A, LDA, S, U, LDU, VT, LDVT, WORK, LWORK, IWORK, INFO)
  DEALLOCATE(WORK)
END SUBROUTINE LAPACK_SING_VALS
